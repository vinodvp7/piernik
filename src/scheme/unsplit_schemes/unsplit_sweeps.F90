!
! PIERNIK Code Copyright (C) 2006 Michal Hanasz
!
!    This file is part of PIERNIK code.
!
!    PIERNIK is free software: you can redistribute it and/or modify
!    it under the terms of the GNU General Public License as published by
!    the Free Software Foundation, either version 3 of the License, or
!    (at your option) any later version.
!
!    PIERNIK is distributed in the hope that it will be useful,
!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!    GNU General Public License for more details.
!
!    You should have received a copy of the GNU General Public License
!    along with PIERNIK.  If not, see <http://www.gnu.org/licenses/>.
!
!    Initial implementation of PIERNIK code was based on TVD split MHD code by
!    Ue-Li Pen
!        see: Pen, Arras & Wong (2003) for algorithm and
!             http://www.cita.utoronto.ca/~pen/MHD
!             for original source code "mhd.f90"
!
!    For full list of developers see $PIERNIK_HOME/license/pdt.txt
!
#include "piernik.h"

!>
!! \brief Module that implements a single sweep
!!
!! \details Here we perform 1D-solves on all blocks including proper exchange of f/c fluxes.
!!
!! When a given block has any boundary with the coarse region, all flux data
!! that has to be sent right after calculation is finished. When a given block
!! has any boundary with the finer region, its calculation is delayed until all
!! fine flux data is delivered. All communication is done quite asynchronously
!! in hope that it will nicely overlap with calculations. In some pessimistic cases
!! long stalls still may occur.
!!
!! This one is the most difficult to aggregate messages carrying flux data on fine/coarse interfaces as
!! there are complicated dependencies between grids. It is possible to calculate which fine grids should be
!! computed first in order to make critical fluxes available as early as possible.
!<

module unsplit_sweeps

! pulled by ANY

   implicit none

   private
   public :: unsplit_sweep

contains

   subroutine update_boundaries(istep)

      use all_boundaries, only: all_fluid_boundaries
!      use cg_leaves,      only: leaves
      use constants,      only: first_stage, DIVB_HDC,xdim,zdim, UNSPLIT
      use domain,         only: dom
      use global,         only: sweeps_mgu, integration_order, divB_0_method, which_solver_type
#ifdef MAGNETIC
      use all_boundaries, only: all_mag_boundaries
#endif /* MAGNETIC */

      implicit none

      integer,                  intent(in) :: istep

      integer(kind=4)                      :: ub_i

      do ub_i=xdim,zdim
            if (dom%has_dir(ub_i)) then
               if (sweeps_mgu) then
                  if (istep == first_stage(integration_order)) then
                     call all_fluid_boundaries(nocorners = .true., dir = ub_i)
                  else
                     call all_fluid_boundaries(nocorners = .true.)
                  endif
               else
                  ! nocorners and dir = cdim can be used safely only when ord_fluid_prolong == 0 .and. cc_mag
                  ! essential speedups here are possible but it requires c/f boundary prolongation that does not require corners

                  ! if (istep == first_stage(integration_order)) then
                  !    call all_fluid_boundaries(nocorners = .true.)
                  ! else
                     call all_fluid_boundaries(istep=istep) !(nocorners = .true., dir = cdim)
                  ! endif
               endif
            endif
         enddo
      if (divB_0_method == DIVB_HDC) then
#ifdef MAGNETIC
         if (which_solver_type==UNSPLIT) then
            call all_mag_boundaries(istep) ! ToDo: take care of psi boundaries
         else
            call all_mag_boundaries ! ToDo: take care of psi boundaries
         endif
#endif /* MAGNETIC */
      endif

   end subroutine update_boundaries

   subroutine unsplit_sweep()

      use cg_cost_data,             only: I_MHD, I_REFINE
      use cg_leaves,                only: leaves
      use cg_list,                  only: cg_list_element
      use cg_list_dataop,           only: cg_list_dataop_t
      use constants,                only: first_stage, last_stage, INVALID,PPP_CG
      use fc_fluxes,                only: initiate_flx_recv, recv_cg_finebnd, send_cg_coarsebnd
      use global,                   only: integration_order
      use grid_cont,                only: grid_container
      use MPIF,                     only: MPI_STATUS_IGNORE
      use MPIFUN,                   only: MPI_Waitany
      use mpisetup,                 only: err_mpi
      use ppp,                      only: ppp_main
      use pppmpi,                   only: req_ppp
      use sources,                  only: prepare_sources
      use solvecg_unsplit,          only: solve_cg_unsplit

      implicit none

      integer                          :: istep
      type(cg_list_element), pointer   :: cgl
      type(grid_container),  pointer   :: cg
      type(cg_list_dataop_t), pointer  :: sl
      type(req_ppp)                    :: req
      logical                          :: all_processed, all_received
      integer                          :: blocks_done
      integer(kind=4)                  :: n_recv, g
      character(len=*), parameter :: solve_cgs_label = "solve_bunch_of_cg", cg_label = "solve_cg", init_src_label = "init_src"


      sl => leaves%prioritized_cg(INVALID, covered_too = .true.)
      ! We can't just skip the covered cg because it affects divvel (or
      ! other things that rely on data computed on coarse cells and are not
      ! restricted from fine blocks).
!      sl => leaves%leaf_only_cg()

      call ppp_main%start(init_src_label)
      ! for use with GLM divergence cleaning we also make a copy of b and psi fields
      cgl => leaves%first
      do while (associated(cgl))
         call prepare_sources(cgl%cg)
         cgl => cgl%nxt
      enddo
      call ppp_main%stop(init_src_label)

      ! This is the loop over Runge-Kutta stages
      do istep = first_stage(integration_order), last_stage(integration_order)

         call initiate_flx_recv(req, INVALID)
         n_recv = req%n
         all_processed = .false.

         do while (.not. all_processed)
            all_processed = .true.
            blocks_done = 0
            ! OPT this loop should probably go from finest to coarsest for better compute-communicate overlap.
            cgl => sl%first

            call ppp_main%start(solve_cgs_label)
            do while (associated(cgl))
               cg => cgl%cg
               call cg%costs%start

               if (.not. cg%processed) then
                  call recv_cg_finebnd(req, INVALID, cg, all_received)
                  if (all_received) then
                     call ppp_main%start(cg_label, PPP_CG)
                     call cg%costs%stop(I_REFINE)
                     ! The recv_cg_finebnd and send_cg_coarsebnd aren't MHD, so we should count them separately.
                     ! The tricky part is that we need to fit all the switching inside the conditional part
                     ! and don't mess pairing and don't let them to nest.

                     call cg%costs%start
                     call solve_cg_unsplit(cg, istep)
                     call cg%costs%stop(I_MHD)

                     call ppp_main%stop(cg_label, PPP_CG)

                     call cg%costs%start
                     call send_cg_coarsebnd(req, INVALID, cg)
                     blocks_done = blocks_done + 1
                  else
                     all_processed = .false.
                  endif
               endif

               call cg%costs%stop(I_REFINE)
               cgl => cgl%nxt
            enddo
            call ppp_main%stop(solve_cgs_label)

            if (.not. all_processed .and. blocks_done == 0) then
               if (n_recv > 0) call MPI_Waitany(n_recv, req%r(:n_recv), g, MPI_STATUS_IGNORE, err_mpi)
               ! g is the number of completed operations
            endif
         enddo

         call req%waitall("unsplit_sweeps")

         call update_boundaries(istep)
      enddo

      call sl%delete
      deallocate(sl)

   end subroutine unsplit_sweep

end module unsplit_sweeps
